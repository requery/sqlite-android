import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'de.undercouch.download'
    id 'com.android.library'
    id 'com.vanniktech.maven.publish' version '0.25.1'
}

android {
    buildToolsVersion "33.0.2"
    ndkVersion '25.2.9519653'
    compileSdkVersion 33

    namespace "io.requery.android.sqlite"

    defaultConfig {
        minSdkVersion 19
        versionName VERSION_NAME

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

dependencies {
    api 'androidx.core:core:1.9.0'
    api 'androidx.sqlite:sqlite:2.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}

ext {
    sqliteDistributionUrl = 'https://www.sqlite.org/2023/sqlite-amalgamation-3410200.zip'
}

task downloadSqlite(type: Download) {
    src project.sqliteDistributionUrl
    dest 'src/main/jni/sqlite.zip'
}

task installSqlite(dependsOn: downloadSqlite, type: Copy) {
    from zipTree(downloadSqlite.dest).matching {
        include '*/sqlite3.*'
        eachFile { it.setPath(it.getName()) }
    }
    into 'src/main/jni/sqlite'
}

preBuild.dependsOn installSqlite

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    failOnError false
}

// https://issuetracker.google.com/issues/207403732
tasks.whenTaskAdded { task ->
    if (task.name.startsWith("configureNdkBuildDebug")
            || task.name.startsWith("configureNdkBuildRelease")) {
        task.dependsOn preBuild
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.DEFAULT, /* automaticRelease */ true)
    signAllPublications()
}
